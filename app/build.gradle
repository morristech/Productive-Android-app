apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../config/quality.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "co.infinum.productive"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
        testApplicationId "co.infinum.productive.instrument.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            keyAlias 'productive-release'
            storeFile file('productive.jks')
            keyPassword '4svF6TfdMgZGVvcw6Sda'
            storePassword 'GhJya8zY4esMZACA6fAs'
        }
    }

    def STRING = 'String'
    def BOOLEAN = 'boolean'
    def TRUE = 'true'
    def FALSE = 'false'
    def API_URL = 'API_URL'
    def ENABLE_ANALYTICS = 'ENABLE_ANALYTICS'
    def ENABLE_CRASHLYTICS = 'ENABLE_CRASHLYTICS'

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            buildConfigField BOOLEAN, ENABLE_ANALYTICS, FALSE
            buildConfigField BOOLEAN, ENABLE_CRASHLYTICS, FALSE
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            versionNameSuffix getBuildNumberSuffix()
            buildConfigField BOOLEAN, ENABLE_ANALYTICS, TRUE
            buildConfigField BOOLEAN, ENABLE_CRASHLYTICS, TRUE
        }
    }

    flavorDimensions 'api'

    productFlavors {
        staging {
            dimension 'api'
            applicationId 'co.infinum.productive.staging'
            buildConfigField STRING, API_URL, '"https://productive-staging.infinum.co/"'
        }
        production {
            dimension 'api'
            buildConfigField STRING, API_URL, '"https://productive.io/"'
        }
    }

    lintOptions {
        // if true, stop the gradle build if errors are found
        abortOnError true
        // turn off checking the given issue id's
        disable 'TypographyFractions', 'TypographyQuotes', 'RtlHardcoded', 'RtlCompat', 'RtlEnabled', 'RtlSymmetry', 'InvalidPackage',
                'MergeRootFrame', 'InconsistentLayout', 'ContentDescription'
        // warning severity
        warning 'StringFormatInvalid', 'AppCompatResource'
        // if true, don't include source code lines in the error output
        noLines false
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

configurations.all {
    resolutionStrategy {
        // should fix support-annotations versions mismatch between app and test app
        force 'com.android.support:support-annotations:23.1.0'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.jakewharton.timber:timber:4.0.1'
    compile 'com.ivankocijan:MagicViews:3.0.0@aar'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'joda-time:joda-time:2.8.2'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    compile 'org.glassfish:javax.annotation:10.0-b28'

    compile('com.afollestad.material-dialogs:core:0.8.4.2@aar') {
        transitive = true
    }

    // unit tests
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    testCompile 'org.robolectric:shadows-play-services:3.0'
    testApt 'com.google.dagger:dagger-compiler:2.0'
    testCompile 'com.squareup.okhttp:mockwebserver:2.5.0'
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.assertj:assertj-android:1.1.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

/**
 * @return a build number suffix if this build has the CIRCLE_BUILD_NUM environment variable set, an empty string otherwise
 */
def getBuildNumberSuffix() {

    def buildNumber = System.getenv("CIRCLE_BUILD_NUM")

    if (buildNumber?.trim()) {
        return '-b' + buildNumber
    } else {
        return ''
    }
}

// Configure robolectric tests.
android.testOptions.unitTests.all {

    include '**/*Test.class'
    exclude '**/espresso/**/*.class'

    // Configure max heap size of the test JVM
    maxHeapSize = '2048m'

    // Configure the test JVM arguments - Does not apply to Java 8
    jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'

    // Specify max number of processes (default is 1)
    maxParallelForks = 1

    // Specify max number of test classes to execute in     a test process
    // before restarting the process (default is unlimited)
    forkEvery = 150

    // configure whether failing tests should fail the build
    ignoreFailures false

    // use afterTest to listen to the test execution results
    afterTest { descriptor, result ->
        def classNameParts = descriptor.className.tokenize('.')
        def simpleClassName = classNameParts[classNameParts.size - 1]
        println "Executed test ${simpleClassName}.${descriptor.name}: ${result.resultType}"
    }
}